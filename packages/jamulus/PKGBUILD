# Maintainer: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Julien Taverna <jujudusud gmail com>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgbase=jamulus
pkgname=(jamulus jamulus-headless)
pkgver=3.8.2
pkgrel=3
pkgdesc="Internet jam session software"
arch=(x86_64 aarch64)
url='https://jamulus.io/'
license=(GPL2 BSD custom:STK)
depends=(gcc-libs qt5-base)
provides=(jamulus-server)
conflicts=(jamulus-git)
makedepends=(jack)
groups=(pro-audio)
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/jamulussoftware/${pkgbase}/archive/r${pkgver//./_}.tar.gz"
'jamulus.service'
'jamulus.sysusers')
sha512sums=('8630cbcbfbdc2051ecbbd5158211efce2f2e47ae18413cb7e549bbe8909110ff13ab8ac2dbacd8c41a88b38d9988ea7c3955a6fec25a1eb18170ab6d792063c7'
            'b958ade056119afc3822ef857c79714e3826009babe402b804777dbf44ece20f4f7bc1329363f748b8c5e8c948f8d1dbb5c0719be25acd9e80ce9350fcaead40'
            'a5863757f935718d0ea90940b0d3406be2111a727ce936c67390c09aa78f6f5925c0bbca1d9d5f9d2c1f74b761d5da43ad00b0684f464bcb3e6c875bf2791e70')
_pkgsrc=${pkgbase}-r${pkgver//./_}

build() {
  cd ${_pkgsrc}
  qmake "CONFIG+=nosound headless" TARGET=jamulus-headless
  make clean
  make
  qmake "CONFIG+=noupcasename"
  make clean
  make
}

package_jamulus() {
  cd ${_pkgsrc}
  pkgdesc+=" (client and server)"
  depends+=(libjack.so)
  install -vDm755 jamulus -t "$pkgdir"/usr/bin
  install -vDm644 ChangeLog README.md -t "$pkgdir"/usr/share/doc/${pkgbase}
  install -vDm644 COPYING -t "$pkgdir"/usr/share/licenses/${pkgbase}
  install -vDm644 distributions/jamulus{,-server}.desktop -t "$pkgdir"/usr/share/applications
  install -vDm644 distributions/jamulus{,-server}.svg -t "$pkgdir"/usr/share/pixmaps
  install -vDm644 ../jamulus.service -t "$pkgdir"/usr/lib/systemd/system
  install -vDm644 ../jamulus.sysusers "$pkgdir"/usr/lib/sysusers.d/${pkgbase}.conf
}

package_jamulus-headless() {
  cd ${_pkgsrc}
  pkgdesc+=" (headless server)"
  conflicts+=(jamulus)
  install -vDm755 jamulus-headless "$pkgdir"/usr/bin/jamulus
  install -vDm644 ChangeLog README.md -t "$pkgdir"/usr/share/doc/${pkgbase}
  install -vDm644 COPYING -t "$pkgdir"/usr/share/licenses/${pkgbase}
  install -vDm644 ../jamulus.service -t "$pkgdir"/usr/lib/systemd/system
  install -vDm644 ../jamulus.sysusers "$pkgdir"/usr/lib/sysusers.d/${pkgbase}.conf
}
